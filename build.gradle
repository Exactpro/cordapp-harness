buildscript {
    def confFile = '.secrets'
    Properties conf = new Properties()
    if (file(confFile).exists()) {
        file(confFile).withInputStream { conf.load(it) }
        ext.artifactory_user = conf.getProperty("CORDA_ARTIFACTORY_USERNAME")
        ext.artifactory_password = conf.getProperty("CORDA_ARTIFACTORY_PASSWORD")
    } else {
        ext.artifactory_user = System.getenv("CORDA_ARTIFACTORY_USERNAME")
        ext.artifactory_password = System.getenv("CORDA_ARTIFACTORY_PASSWORD")
    }
    ext {
        corda_release_group = "com.r3.corda"
        corda_release_version = "4.2"
        corda_gradle_plugins_version = "4.0.47"
        quasar_version = "0.7.11_r3"   // quasar-utils plugin reads it
        log4j_version = "2.11.2"
        corda_platform_version = 4
        artifactoryUrl = "https://software.r3.com/artifactory"

        testCordapp1 = "com.r3.corda:corda-ptflows:4.2"
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url "$artifactoryUrl/corda" }
    }

    dependencies {
        classpath "net.corda.plugins:cordapp:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:cordformation:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:quasar-utils:$corda_gradle_plugins_version"
    }
}

allprojects {
    repositories {
    //mavenLocal()
      mavenCentral()
      jcenter()
      maven { url "$artifactoryUrl/corda" }
      maven { url "$artifactoryUrl/corda-enterprise"
        credentials {
          username = artifactory_user
          password = artifactory_password
        }
      }
    }
}

apply plugin: 'net.corda.plugins.cordformation'
apply plugin: 'net.corda.plugins.quasar-utils'

dependencies {
    cordaRuntime "$corda_release_group:corda-node-api:$corda_release_version"
    cordaRuntime "$corda_release_group:corda:$corda_release_version"
    cordaRuntime "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"
    cordapp "$testCordapp1"
}

task deployNodes(type: net.corda.plugins.Cordform) {
    nodeDefaults {
	projectCordapp { deploy = false }  // don't look for the phantom CorDapp of this project
        cordapp("$testCordapp1")
    }
    node {
        name "O=Party1, L=Rome, C=IT"
        notary = [validating : false]
        p2pPort 10011
        rpcSettings {
            address("0.0.0.0:10012")
            adminAddress("localhost:10013")
        }
     // extraConfig = [ 'sshd.port':'10014' ]
        rpcUsers = [[ user: "userN", "password": "z", "permissions": ["ALL"]]]
     // cordapp("$testCordapp1")
    }
    node {
        name "O=Party2, L=Oslo, C=NO"
        notary = [validating : false]
        p2pPort 10021
        rpcSettings {
            address("0.0.0.0:10022")
            adminAddress("localhost:10023")
        }
        rpcUsers = [[ user: "userN", "password": "x", "permissions": ["ALL"]]]
    }
}
